/**
 *
 * Drifting for TrackMania 2020
 *
 * written by dassschaf / UD Timmy
 *
 */

// Extends base mode script of TM 2020:
#Extends "Libs/Nadeo/TMNext/TrackMania/Modes/TMNextBase.Script.txt"

// ---------------------------------- //
// Script Info
#Const	CompatibleMapTypes	"TrackMania\\TM_Race,TM_Race"
#Const  Version     		"0"
#Const  ScriptName  		"Drift.Script.txt"
#Const  C_ModeName          "Drift"
#Const  Description         _("In $<$Drift mode$>, the goal is to make as much points as possible through drifting over the track. The higher your angle of attack and speed are, the higher the points you gain. The winner is the player with most points at the end of the round.")

// ---------------------------------- //
// Libraries
//
// Utility libraries:
#Include "MathLib" as MathLib
#Include "TextLib" as TextLib

// Various libraries:
#Include "Libs/Nadeo/ModeLibs/Legacy/Layers2.Script.txt" as Layers

// UI-Modules:
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/ScoresTable_Server.Script.txt" as UIModules_ScoresTable
#Include "ManiaApps/Nadeo/TMxSM/Race/UIModules/PauseMenuOnline_Server.Script.txt" as UIModules_PauseMenu_Online

// ---------------------------------- //
// Settings
// #Setting S_NAME VALUE as _("TEXT")

#Setting S_TimeLimit            300         as _("Time Limit:")
#Setting S_ShowDebugInfo        True        as _("Show Debug info")
#Setting S_EnableCollisions     True        as _("Enable Collisions between cars")
#Setting S_RespawnPenalty       500         as _("Respawn Penalty in pts")
#Setting S_CollisionPenalty     0           as _("Collision Penalty in pts")

// ---------------------------------- //
// Constants
// #Const C_NAME VALUE
#Const C_UploadRecord False
#Const C_HudModulePath "" //< Path to the hud module
#Const C_ManiaAppUrl "file://Media/ManiaApps/Nadeo/TMNext/TrackMania/Rounds/Rounds.Script.txt" //< Url of the mania app
#Const C_FakeUsersNb 0

// ---------------------------------- //
// Structures
#Struct K_PlayerInfo {
    Text Login;             //< Player Login for identification

    Integer CurrentScore;   //< current run's score
    Boolean IsSkidding;     //< whether player is drifting or not
    Real CurrentSkidScore;  //< score of the current drift, added after drift end, discarded if collision
    Real LastTickSpeed;     //< Player.Speed from last tick, used for determining collisions
}

// ---------------------------------- //
// Global variables 
declare K_PlayerInfo[] G_PlayerInfo; // player info array


// ---------------------------------- //
// Extends

***Match_LogVersion***
***
    // register the script to the log
    Log::RegisterScript(ScriptName, Version);
*** // Match_LogVersion

***Match_Settings***
***
    MB_Settings_UseDefaultHud = (C_HudModulePath == "");
***

***Match_Rules***
***
    // register game mode information
    ModeInfo::SetName(C_ModeName);
    ModeInfo::SetType(ModeInfo::C_Type_FreeForAll);
    ModeInfo::SetRules(Description);
    ModeInfo::SetStatusMessage(_("TYPE: Free for all\nOBJECTIVE: Set the best score on the track."));

*** // Match_Rules

***Match_LoadHud***
***
    if (C_HudModulePath != "") Hud_Load(C_HudModulePath);
***

***Match_AfterLoadHud***
***
    ClientManiaAppUrl = C_ManiaAppUrl;
    Race::SortScores(Race::C_Sort_TotalPoints);
    UIModules_ScoresTable::SetScoreMode(UIModules_ScoresTable::C_Mode_Points);
    UIModules_PauseMenu_Online::SetHelp(Description);

    // Hide SM Overlay
    UIManager.UIAll.OverlayHideSpectatorControllers = True;
    UIManager.UIAll.OverlayHideSpectatorInfos = True;
    UIManager.UIAll.OverlayHideChrono = True; 
    UIManager.UIAll.OverlayHideCountdown = True;
***

***Match_StartServer***
***    
    // Gamemode settings
    UsePvPCollisions = S_EnableCollisions;


*** // Match_StartServer



***Match_InitMap***
***
    // clear info array
    G_PlayerInfo.clear();

    foreach (Player in Players)
        G_PlayerInfo.add(K_PlayerInfo{Login = Player.User.Login, CurrentScore = 0, IsSkidding = False, CurrentSkidScore = 0., LastTickSpeed = 0.});
        
    declare Integer Map_TimeLimit;
    declare Integer Map_MapStartTime;
    
*** // Match_InitMap



***Match_StartMap***
***
    // Map actual start
    StartTime = Now + Race::C_SpawnDuration;
    Map_TimeLimit = S_TimeLimit;
    Map_MapStartTime = StartTime;
    SetTimeLimit(StartTime, S_TimeLimit);

    // Start players for the race
    foreach (Player in Players) {
        Race::Start(Player);
    }

*** // Match_StartMap



***Match_InitTurn***
***
    // Turn initialitzation
    yield;

*** // Match_InitTurn



***Match_StartTurn***
***
    // Turn start
    yield;

*** // Match_StartTurn



***Match_PlayLoop***
***
    // actual game mechanics?!

    // ------------------
    // Pending Events Handler
    foreach (Event in PendingEvents) {
        declare Player <=> Event.Player;
        declare Integer PId = GetPlayerIndexFromLogin(Player.User.Login);

        switch (Event.Type) {
            // Player joins game:
            case CSmModeEvent::EType::OnPlayerAdded: {
                G_PlayerInfo.add(K_PlayerInfo{Login = Player.User.Login, CurrentScore = 0, IsSkidding = False, CurrentSkidScore = 0., LastTickSpeed = 0.});
            }

            // Player traverses finish or lap
            case CSmModeEvent::EType::OnPlayerTriggersWaypoint: {
                if (Event.IsFinish || Event.IsNewLap) {
                    // player passed finish line, score handling here
                    
                    // add ongoing skid to score
                    G_PlayerInfo[PId].CurrentScore += MathLib::NearestInteger(G_PlayerInfo[PId].CurrentSkidScore);

                    // apply score to the actual score list if better
                    if (G_PlayerInfo[PId].CurrentScore > Player.Score.Points)
                        Player.Score.Points = G_PlayerInfo[PId].CurrentScore;
                    
                    // reset the score variables
                    G_PlayerInfo[PId].CurrentScore = 0;
                    G_PlayerInfo[PId].CurrentSkidScore = 0.;
                    G_PlayerInfo[PId].IsSkidding = False;
                }
            }

            // Player requests respawn
            case CSmModeEvent::EType::OnPlayerRequestRespawn: {
                // player respawns

                // reset ongoing skid
                ResetOngoingSkid(PId, S_RespawnPenalty);
            }
        }


    }

    // ------------------
    // Manage per-player actions
    foreach (Player in Players) {
        declare Integer PId = GetPlayerIndexFromLogin(Player.User.Login);

        // skip loop iteration if info struct not found
        if (PId == -1)
            continue;

        // re-spawning
        if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned) {   
            // spawn player
            Race::Start(Player);
        
            // reset skid and run score
            G_PlayerInfo[PId].CurrentScore = 0;
            ResetOngoingSkid(PId, 0);
        }

        // reading out physics
        if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned) {

            // fetch skidding information
            declare Integer SkiddingDuration = Player.SkiddingDuration;
            declare Real SkiddingDistance = Player.SkiddingDistance;
            declare Integer WheelsSkiddingCount = Player.WheelsSkiddingCount;
            declare Integer WheelsContactCount = Player.WheelsContactCount;
            declare Real Speed = Player.Speed;

                   
            // score relevant information
            declare Real SkidAngle; //<- radians, 3.14 == 180° turned skiddderidoo
            declare Real Points;

            // prelimiary collision check
            if (MathLib::Abs(G_PlayerInfo[PId].LastTickSpeed - Speed) > 12)
                ResetOngoingSkid(PId, S_CollisionPenalty);

            G_PlayerInfo[PId].LastTickSpeed = Speed;
            

            // score calculation
            // check if not airborne
            if (WheelsContactCount >= 2 && WheelsSkiddingCount >= 2 || WheelsContactCount == 4) {
                SkidAngle = MathLib::Abs(MathLib::OrientedAngle(Player.Velocity, Player.AimDirection));

                // Check for sufficient skid angle and speed (m/s)
                // if the car's angle between velocity and aim vector is HIGHER than 135° it is not a valid drift
                // to prevent driving backwards as meta strategy
                if (SkidAngle > 0.1 && SkidAngle < 2.35 && Speed > 12 || WheelsSkiddingCount > 0 && SkidAngle < 2.35) {
                    // car is skidding, calculate points
                    G_PlayerInfo[PId].IsSkidding = True;
                    Points = SkidAngle * Speed * 0.05;
                    G_PlayerInfo[PId].CurrentSkidScore += Points;
                
                } else {
                    // car is not skidding
                    // add skid score to run score
                    if (G_PlayerInfo[PId].IsSkidding)
                        G_PlayerInfo[PId].CurrentScore += MathLib::NearestInteger(G_PlayerInfo[PId].CurrentSkidScore);
                        
                    // reset values
                    ResetOngoingSkid(PId, 0);
                }
    
            } else {

                // car does not qualify for skidding, car airborne
                if (G_PlayerInfo[PId].IsSkidding)
                    G_PlayerInfo[PId].CurrentScore += MathLib::NearestInteger(G_PlayerInfo[PId].CurrentSkidScore);
                
                // reset values
                ResetOngoingSkid(PId, 0);
                SkidAngle = 0.0;
                Points = 0.0;
            }

            if (S_ShowDebugInfo) {
                // car velocity
                declare Real VelX = Player.Velocity.X;
                declare Real VelY = Player.Velocity.Y;
                declare Real VelZ = Player.Velocity.Z;

                // car direction
                declare Real AimX = Player.AimDirection.X;
                declare Real AimY = Player.AimDirection.Y;
                declare Real AimZ = Player.AimDirection.Z;

                // player input
                declare Real InputSteer = Player.InputSteer;

                // make manialink
                declare Text ManialinkDebugInfo;
                ManialinkDebugInfo = """
                <label pos="0 1" z-index="0" 
                text="
                Skidding? {{{G_PlayerInfo[PId].IsSkidding}}}
                Speed: {{{Speed}}}
                Skid Duration: {{{SkiddingDuration}}}
                Skid Distance: {{{SkiddingDistance}}}
                Skid Wheels # {{{WheelsSkiddingCount}}}
                Grnd Wheels # {{{WheelsContactCount}}}
                Vel Direction: X={{{VelX}}}, Y={{{VelY}}}, Z={{{VelZ}}}
                Car Direction: X={{{AimX}}}, Y={{{AimY}}}, Z={{{AimZ}}}
                Skid Angle: {{{SkidAngle}}}
                Points: {{{Points}}}
                Skid Points: {{{G_PlayerInfo[PId].CurrentSkidScore}}}
                Total Points: {{{G_PlayerInfo[PId].CurrentScore}}}
                Current Score: {{{Player.Score.Points}}}
                "/>
                """;

                // get debug info for the player
                Layers::Create("ManialinkDebugInfo", ManialinkDebugInfo);
                Layers::SetType("ManialinkDebugInfo", CUILayer::EUILayerType::Normal);
                Layers::Attach("ManialinkDebugInfo", Player);
            }
        }
    }

    // Spawn players
    if (PlayersNbDead > 0) { //< Check for unspawned players only if at least one player is unspawned
        foreach (Player in Players) {
            declare Integer PId = GetPlayerIndexFromLogin(Player.User.Login);
            if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::NotSpawned && Race::IsReadyToStart(Player)) {
                // reset skid and run score
                G_PlayerInfo[PId].CurrentScore = 0;
                ResetOngoingSkid(PId, 0);

                // respawn player
                Race::Start(Player);
            }
        }
    }

    // Update the map duration setting
    if (Map_TimeLimit != S_TimeLimit) {
        Map_TimeLimit = S_TimeLimit;
        SetTimeLimit(StartTime, S_TimeLimit);
    }

    // End the map when time limit is reached
    if (EndTime > 0 && Now >= EndTime) {
        MB_StopMatch();
    }

*** // Match_PlayLoop

***Match_EndMap***
***

    // Destroy UI layers
    Layers::Destroy("ManialinkDebugInfo");

    MB_StopMatch();

    EndTime = -1;
    Match_MatchDuration = ML::Max(0, Now - Map_MapStartTime);
    StateMgr::ForcePlayersStates([StateMgr::C_State_Waiting]);
    Race::EnableIntroDuringMatch(False);

    Race::SortScores(Scores::C_Sort_MatchPoints);
    TrophyRanking::UpdateUsersRank();
    CarRank::Update(CarRank::C_SortCriteria_BestRace);
    Scores::SetDefaultLadderSort(Scores::C_Sort_MatchPoints);
    Scores::SetPlayerWinner(Scores::GetBestPlayer(Scores::C_Sort_MatchPoints));
    Race::StopLosers();
    MB_Sleep(1000);
    Race::StopSkipOutroAll();


*** // Match_EndMap

// ------------------
/** 
 * @param _PId      Player's ID
 * @param _Penalty  Penalty in pts to be deducted from Player's score
 */
Void ResetOngoingSkid(Integer _PId, Integer _Penalty) {

    // reset ongoing skid
    G_PlayerInfo[_PId].CurrentSkidScore = 0.;
    G_PlayerInfo[_PId].IsSkidding = False;

    // deduct penalty points
    G_PlayerInfo[_PId].CurrentScore -= _Penalty;

    // fix negative scores
    if (G_PlayerInfo[_PId].CurrentScore < 0)
        G_PlayerInfo[_PId].CurrentScore = 0;
}

// ------------------
/**
 * @param _Login    Player's Login
 *
 * @return          Player's Index of his data struct in G_PlayerInfo
 */
Integer GetPlayerIndexFromLogin(Text _Login) {
    declare Integer PId = -1;

    // Iterate over player info structs, return PId once matching player is found
    for (I, 0, G_PlayerInfo.count - 1) {
        if (G_PlayerInfo[I].Login == _Login)
            PId = I;
    }

    // return -1 if there is no fitting player
    return PId;
}

// ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ //
/** Update the time limit
 *
 *	@param	_StartTime								The starting time of the map
 *	@param	_NewTimeLimit							The time limit before going to the next map
 */
Void SetTimeLimit(Integer _StartTime, Integer _NewTimeLimit) {
	if (_NewTimeLimit <= 0) {
		EndTime = -1;
	} else {
		EndTime = _StartTime + (S_TimeLimit * 1000);
	}
}